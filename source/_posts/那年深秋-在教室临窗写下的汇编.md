---
title: '那年深秋,在教室临窗写下的汇编'
top: false
cover: false
toc: true
mathjax: true
date: 2021-07-12 12:00:52
password:
summary:
tags: intel8086汇编
categories: 计算机基础
---

## 前言

### 1. 为什么要学习汇编语言？

编程语言发展到如今，**高级语言**大行其道，他们通常被标榜为学习成本低。但由于高级语言不是直接调用**操作系统**，因而**执行效率**相对低一些。

比如java语言，java源文件首先被编译，然后经过**JVM**的连接，最终执行得到运行于某平台的**机器码**。也就是说最终被计算机执行的是机器码。

但机器码是不易于人理解的，虽说大道至简，在计算机的世界里，**0,1**才是王道。但计算机的模型通过0,1是很难窥见的，因而我们需要一种即易于人理解，且能够了解计算机执行机制的语言，这便是——**汇编语言**。

#### C语言与汇编

直观的看，操作系统的百分之九十代码是用C语言写的，数据库等软件同样是使用C语言编写的。也就是说，C语言执行效率高，适合用于描述底层的东西。

我们可以把**工作技能**比作裁缝做成的**衣服**，java是衬衫，C++是长裤，但只会做衬衫的裁缝可不是一个好裁缝，裁缝铺需要的是，根据顾客的要求，`不管什么服装都可以做的好裁缝`。

但这可不容易，我们需要了解不同类型衣服的共同之处，而衣服都是由**裁缝机**做成的，那么裁缝机是什么样，也会一定程度决定做成的衣服是什么样(`PS:这里我们不讨论先有鸡还是先有蛋的问题`)。

这就如同语言，语言最终是去驱使硬件工作的，硬件也会反过来决定语言的特性，硬件的工作状态是高，低电平，对应计算机语言的0和1，所以哪种语言能够更好的解释0和1，而又为人所理解，那就会最先被衍生出来。

这就是大家心心念念的C语言了，C语言向上沟通众多高级语言，无往不利，向下连通"取指执行"的圣经。

而C语言要描述0和1，可不是直接编译那么简单，它要首先编辑为汇编语言，最终被连接执行为机器语言。

### 2 . 汇编语言的定义

> 汇编语言是二进制指令的文本形式

### 3 . 学习汇编的要求

通过使用**汇编指令**对**寄存器**和**内存**的操作来深刻理解**计算机执行指令**的机制。

[^undefined]: 注 ： 我们这里的汇编专指8086CPU的汇编语言

## 基础知识

### 1. 为什么要使用寄存器

CPU本身只负责**运算**，不负责**存储数据**，数据一般都存储在**内存**之中，CPU要用的时候就去内存读写数据，但是CPU读写的速度**远高于**内存的读写速度，为了避免被拖慢，CPU都自带一级缓存和二级缓存，**CPU缓存**可以看作是读写速度较快的内存。

但是，CPU缓存还是不够快，另外数据在缓存里面的地址是**不固定**的，CPU每次读写都要**寻址**也会拖慢速度，因此，除了缓存之外，CPU还自带了**寄存器**，用来存储最常用的数据。也就是说，那些最频繁读写的数据，比如循环变量，都会放在寄存器中，CPU优先读写寄存器，再由寄存器跟内存交换数据

![image-20201103224649594](https://gitee.com//future727/imgs/raw/master/zyz_img2/image-20201103224649594.png)

寄存器不依靠地址区分数据，而依靠名称。每一个寄存器都有自己的名称，我们告诉CPU去具体哪一个寄存器中拿数据，这样的速度是最快的，因而也称寄存器是CPU的**零级缓存**。

------

### 2.寄存器的概述

8086的寄存器都是16位的，可以存放两个字节的数据，也就是说它的寻址能力就只有2的16次方个字节空间，也就是64KB。

通俗的将，32位，64位系统指的就是寄存器的位数。32位寄存器可以存放的数据是2的32次方，也就是4GB，换句话说，32位的系统最多能寻址的数据只有4GB。

寄存器要存储数据，首先需要从内存中取出数据，而从内存中读取数据，就要知道数据在内存中存储的地址，也就是物理地址。

### 3.物理地址

8086CPU有20位的地址总线，可以传送20位地址，达到1MB寻址能力，但数据总线只有16位，它只能传输16的地址，表现出来的寻址能力只有64KB，所以我们需要使用两个16位的地址来合成20位的物理地址。

这两个16位的地址，一个是段地址CS，一个是偏移地址IP，采用物理地址=短地址*16+偏移地址的运算方式扩展16位到20位，换句话说，也就是逻辑左移4位。

有了物理地址，我们就可以知道从哪个内存地址读取指令，那如何将取出来的指令执行呢？

![image-20201108221905157](https://gitee.com//future727/imgs/raw/master/zyz_img2/image-20201108221905157.png)

了解了指令执行的模型，我们知道CS,IP的值是从寄存器中读取的，那寄存器是怎样存储值的呢？

### 4.寄存器的分类

从类型上说，我们存储的数据有两类，字节型和字型。

而从数据类型来说，数据分为通用的数据和具有特定意义的数据，在计算时，通用的寄存器存储在通用寄存器中，而特殊的数据，比如需要保留状态的数据，会被存储在标志寄存器中。下面我们来看具体的描述——

8086CPU有8个寄存器，且都是16位的，即可以存放两个字节，8个寄存器中包括4个通用寄存器，4个段寄存器。

4个通用的寄存器分别是AX,BX,CX,DX,用于存放一般性的数据。而考虑到兼容，又将这四个寄存器分成了独立的8个寄存器来使用。比如取AX的低八位组成了AL寄存器，高八位组成了AH寄存器。

当CPU要访问内存时，由段寄存器来提供内存单元的段地址，这些寄存器包括DS,ES,SS,CS.

其中DS寄存器通常用于存放要访问数据的段地址，CS寄存器是代码段寄存器，当我们要访问栈段时，栈段的起始地址从SS寄存器中读取，偏移地址从SP寄存器中读取，ES为扩展段寄存器。

### 2. 内存模型

寄存器只能存放少量的数据，大多数时候，CPU要指挥寄存器与内存进行数据交换，所以，除了寄存器，还必须了解内存如何存储数据。

当程序运行时，操作系统会为程序分配一段内存空间，用于存储程序和程序运行产生的结果。这段内存空间有一个起始地址和一个结束地址。且低位的是起始地址，高位的是结束地址。

通俗的说，内存中存放的是源程序，源程序中有效的部分称为程序，而程序是由数据和指令组成的，那内存是如何区分数据和指令的呢？CS:IP指向的内存单元存放的是指令，其他的则是数据。但数据的使用频度是不同的，使用场景也是不同的，因此我们把一般性的数据存放在堆中，而把需要保存状态的数据存放在栈中，那堆栈有怎样的结构特点呢——

### 堆

程序运行过程中，对于动态的内存占用请求，比如说新建对象，或者使用malloc命令，系统就会从预先分配好的那段内存找那个，划出一部分给用户，规则是从起始地址算起，而实际上起始地址会有一段静态数据，这里暂且忽略。

这种因为用户主动请求而划分出来的内存区域，叫做heap（堆），它由起始地址开始向高位增长，Heap的一个重要特点就是不会自动消失，必须手动的释放或者通过内存垃圾回收机制来回收。

除了Heap以外，其他的内存占用叫做Stack(栈)，简单说，栈是函数运行时被临时使用的内存空间。

请看下面的例子——

```java
int main(){
	int a;
	int b;
	return add(a,b);
}
```

当系统开始执行main函数时，会为其在内存中建立一个帧，这个数据帧中会存放main函数中声明的内部变量a,b.而当程序执行到调用add函数时，这时就需要为这个函数也建立一个帧，用来存储它的内部变量。一般来说，调用了多少层的帧，就有多少个栈，等到add函数执行结束，它的帧就会被回收，程序再次回到main函数中断的地方，继续往下执行，系统使用这种机制，实现了函数的层层调用。

所有的帧，都存放在栈中，生成新的帧，叫做入栈，英文是push.将栈回收，叫做出栈,英文是pop,栈的特点就是先进后出，栈顶指针始终指向最外层的帧，帧是从栈的高地址开始存放，之后内存地址不断递减直至栈满。

那栈的空间是由谁来决定的呢？系统能否判断栈是否已满呢？

并不能，当需要使用栈的时候，我们需要声明一段内存空间作为栈段，我们只能人为的避免造成栈满，而不能寄希望于操作系统。

## 常用指令

### 1. 伪指令与指令

程序是指令的集合，每一条指令都对应CPU的操作，但仅有指令是不够的，指令不能告诉编译器，程序什么时候结束，也不能告诉编译器，数据段存储在哪段内存空间，代码段又存储在哪段内存空间。所以需要有一种能被编译器所识别的，用于定义程序结构化的语言。这就是伪指令了。

伪指令是用于告诉汇编程序如何进行**汇编**的指令。它既不控制机器的操作也不被汇编成机器代码，只能为汇编器所识别并指导汇编程序如何进行。 将相对于程序或相对于寄存器的地址载入寄存器中。

### 2 .常用指令

| 序号 | **语法**             | **功能**               |                **案例**                 | **说明**                                                     | **备注**                                                    |
| ---- | -------------------- | ---------------------- | :-------------------------------------: | ------------------------------------------------------------ | ----------------------------------------------------------- |
| 1    | mov                  | 数据传送指令           |                mov[0],cs                | 将寄存器CS中的内容传送到偏移地址为0的内存单元中              | 符合反对称                                                  |
| 2    | add/sub              | 加、减指令             |                sub ax,bx                | 将ax寄存器中的内容减去bx寄存器中的内容的结果存储在ax中       |                                                             |
| 3    | push                 | 入栈                   |                 push ax                 | 分两步，首先栈顶指针偏移，其次将ax寄存器中的内容入栈         | 段地址从ds中取得                                            |
| 4    | pop                  | 出栈                   |                 pop ax                  | 分两步，首先将栈顶指针指向的内容赋给ax寄存器，然后栈顶指针偏移 |                                                             |
| 5    | loop 标号            | 跳转至标号处           |              loop continue              | CPU执行loop指令时，进行两步操作：首先CX递减，然后判断CX中的值，若不为0，则跳转标号处，若为0，则向下执行 | CX专用于存放循环次数                                        |
| 6    | and                  | 逻辑与                 |          and      al,01100011B          | 按位进行与运算                                               | 常用于位清除                                                |
| 7    | or                   | 逻辑或                 |             or al,01100011B             | 按位进行或运算                                               | 常用于位填充                                                |
| 8    | div                  | 除法                   |           div byte ptr ds:[0]           | 做除法操作时，除数有8位和16位两种，存放在在一个reg/内存单元中。被除数默认放在AX或者DX和AX中，如果除数为8位，被除数则为**16位**，默认在**AX**中存放；如果除数为16位，被除数为**32位**，在**DX和AX**中存放，D**X存放高16位，AX存放低16位**。结果：如果除数为8位，则AL储存除法操作的商，AH储存除法操作的余数；如果除数为16位，则AX储存除法操作的商，DX储存除法操作的余数， | 进行除法操作前，首先要进行除数和被除数的设置                |
| 9    | jmp                  | 无条件转移指令         |               jmp short s               | **段内短位移**，转到标号s处执行指令。                        | jmp指令应该包含两种信息：转移的**目的地址**与转移的**距离** |
| 10   |                      |                        |              jmp far ptr s              | **段间转移**，使用S的段地址和偏移地址修改CS和IP              |                                                             |
| 11   |                      |                        |                 jmp AX                  | **段内偏移**，将寄存器AX中的内容赋给IP                       |                                                             |
| 12   |                      |                        |           jmp word ptr ds:[0]           | **段内偏移**，从内存单元的地址处开始存放一个字，即转移的目的**偏移地址** |                                                             |
| 13   | jcxz                 | 有条件转移指令         |                 jcxz s                  | **短位移**，指令中包含转移位移，而不是目的地址。等同于 if(CX == 0) jmp short 标号 |                                                             |
| 14   | call                 | 转移指令               |                 call s                  | 不能实现短位移，执行call指令，执行两步操作：push IP ， jmp  near ptr 标号 |                                                             |
| 15   | ret                  | 复原指令               |                   ret                   | CPU执行ret操作时，等同于执行指令 pop IP                      |                                                             |
| 16   | retf                 | 复原指令               |                  retf                   | CPU执行retf操作时，等同于执行指令pop IP,pop CS               |                                                             |
| 17   | mul                  | 乘法指令               |     mov ax,100 mov bx,10000 mul bx      | 类div指令                                                    |                                                             |
| 18   | adc                  | 带进位加法             |                adc AX,BX                | 等同于add AX,BX, ADD AX,CF                                   |                                                             |
| 19   | sub                  | 带借位减法             |                sub AX,BX                | 等同于sub AX,BX, sub AX,CF                                   |                                                             |
| 20   | cmp                  | 比较指令               | mov ax,8        mov bx,3      cmp ax,bx | 相当于减法操作，计算结果会对标志寄存器产生影响               |                                                             |
| 21   | je jne jb jnb ja jna | 根据比较结果的跳转指令 |                  jxx s                  | 等于/不等于/低于/不低于/大于/不大于                          |                                                             |
| 22   | pushf  popf          |                        |               pushf  popf               | 将标志寄存器中的值压栈/出栈                                  |                                                             |
| 23   | iret                 | 复原指令               |                  iret                   | 等同于：pop IP，pop CS,popf                                  |                                                             |
| 24   | int                  | 中断指令               |                  int n                  | 这里的n表示的是中断类型码。CPU执行int n指令，执行过程：首先取中断类型码n，然后标志寄存器入栈，接着CS，IP入栈。最后跳转执行 |                                                             |
| 25   | shl                  | 逻辑左移指令           |                shl al,1                 | 将al中的数据逻辑左移1位，最后移出的1位写入CF，最低位用0补充  |                                                             |
| 26   | shr                  | 逻辑右移指令           |                shr al,1                 | 将al中的数据逻辑右移1位，最后移出的1位写入CF，最高位用0补充  |                                                             |

### 3.常用的伪指令

| 名称            | 功能                     | 案例            | 说明                                 | 备注 |
| --------------- | ------------------------ | --------------- | ------------------------------------ | ---- |
| dd(double word) | 定义双字型数据           | dd 12345        | 定义了占2个字的数据12345             |      |
| db(define byte) | 定义字节型数据           | db 1            | 定义了占1个字节的数据1               |      |
| dw(define word) | 定义字型数据             | dw 100          | 定义了占1个字的数据100               |      |
| dup             | 用于进行数据的重复       | db 3 dup (0,1,) | 定义了6个字节数据，分别是0,1,0,1,0,1 |      |
| offset          | 取得标号的偏移地址       | mov si,offset s | 将标号s的偏移地址赋给si              |      |
| seg             | 回送变量或者标号的段地址 |                 |                                      |      |

- [ ] 更多的可参考http://blog.sina.com.cn/s/blog_4a92ce12010006m2.html

### 4. 以学促用

#### 问题描述

> 将实验七中的Power idea 公司的数据，按照图10.2所下图屏幕中显示出来

![image-20201112151602560](https://gitee.com//future727/imgs/raw/master/zyz_img2/image-20201112151602560.png)

#### 代码与注释

```ASM
assume cs:codesg
;将整个data段看作是一个数组，长度一共为
;21*4+21*4+2*21=168+42=210字节
data segment
    db '1975', '1976', '1977', '1978', '1979', '1980', '1981', '1982', '1983'
    db '1984', '1985', '1986', '1987', '1988', '1989', '1990', '1991', '1992'
    db '1993', '1994', '1995'
    ;长度为84的数组，一个字符一个字节：4X21

    dd 16, 22, 382, 1356, 2390, 8000, 16000, 24486, 50065, 97479, 140417, 197514
    dd 345980, 590827, 803530, 1183000, 1843000, 2759000, 3753000, 4649000, 5937000
    
    dw 3, 7, 9, 13, 28, 38, 130, 220, 476, 778, 1001, 1442, 2258, 2793, 4037, 5635, 8226
    dw 11542, 14430, 15257, 17800

data ends

;ax+210/16向上取整，即ax+14h
;对于ds，偏移量为16*14=224
table segment
    db 21 dup('year summ ne ?? ')
table ends

show segment
    db 100 dup(0)
show ends

temp segment
    dw 10 dup(0)
temp ends

codesg segment
start:	  	mov ax, data
			mov ds, ax 
			mov bx, table
			mov es, bx  
			mov bx, 0   ;定位结构体数组元素
			mov di, 0   ;定位data中的4字节数据
			mov si, 0   ;定位data中的2字节数据
			mov cx, 15h
s0:			;转移年份
			mov ax, ds:[di]
			mov es:[bx], ax
			mov ax, ds:[di+2]
			mov es:[bx+2], ax
			;添加空格 
			mov byte ptr es:[bx+4], 32

			;转移收入
			;作为被除数
			mov ax, ds:[di+84];
			mov es:[bx+5], ax
			mov ax, ds:[di+86]
			mov es:[bx+7], ax
			;添加空格 
			mov byte ptr es:[bx+9], 32
	
			;转移雇员数量
			mov ax, ds:[si+168]
			mov es:[bx+10], ax
			;添加空格 
			mov byte ptr es:[bx+12], 32
	
			;计算人均收入，并转移到table中
			mov ax, es:[bx+5]   ;低16位
			mov dx, es:[bx+7]   ;高16位
			div word ptr es:[bx+10]
			mov es:[bx+13], ax  ;商默认存放在ax中 
			;添加空格 
			mov byte ptr es:[bx+15], 32
			;操作完成，bx加16，di加2
			add bx, 16  
			add di, 4
			add si, 2
			loop s0

;s0循环的作用主要是将数据存储到table段中
;而在s1循环中，先把table段中的数据转化为字符串，转储到
;show段中，然后再在s2循环中调用show_str子程序来将这些字符串一行一行地
;显示到屏幕上
			mov ax, table
			mov ds, ax
			mov ax, show
			mov es, ax
			mov dx, 0
			mov dh, 4
			mov bx, 0
			mov cx, 15h
s1:			mov di, 0
			;这里的di作为table数据段的指针
			push ds
			push es
			push cx
			push dx
			;保存行列信息，在调用show_str时会用到
			;转储年份
			mov ax, ds:[bx+di]
			mov es:[di], ax
			add di, 2
			mov ax, ds:[bx+di]
			mov es:[di], ax
			mov byte ptr es:[di+4], 0
			;转储收入
			mov di, 5
			mov ax, ds:[bx+di]
			mov dx, ds:[bx+di+2]
			call dtoc 
			;转储人数
			mov ax, ds:[bx+10]
			mov dx, 0
			call dtoc 
			;转储人均收入
			mov ax, ds:[bx+13]
			mov dx, 0
			call dtoc
			;以上就是table段中的一行转换为ASCII码值的处理过程
			

			;下面开始显示过程，此过程需要循环四次
			pop dx			
			mov di, 0
			;这里的di作为待输出数据段的指针
			mov cx, 4

s2:			mov si, 0
			call show_str
			add dl, 20
			;把列错开
			loop s2
			mov dl, 0
			add dh, 1
			add bx, 16
			pop cx
			pop es
			pop ds
			loop s1
			

			mov ax, 4c00h
			int 21h

dtoc:		;该子程序用于将数值型的数字转换为字符串
			;十进制数值转换为ASCII码值，转换关系为：ascii=10进制+30H
			;要想将一个十进制的整数拆分成一个一个
			

			的数值，那我们需要让这个数
			;除以10，然后将得到的结果依次入栈，除完之后再依次出栈，即可得到由高位到低位
			;的所有数值，之后将这些值加上30H，即得到其对应的ASCII码值，然后将这些
			;ASCII码值存放到一个数据段中，调用show_str函数，来在屏幕上显示这些数值
			;为了存储转换后的ASCII码值，我们需要新开辟一个数据段
			push bx
			push cx
			push dx
			push ds 
			push ax
			push si
			push es

  			mov bx, 0
			;记录十进制数据的位数
split:		mov cx, 0ah
			;cx存放除数  
			call divdw
			

			;如果被除数大于2550，al是无法存放商的，会造成溢出，因此，我们需要调用本实验中第二个程序
			;专门用于解决除法溢出问题的程序
			;此程序返回运算后的商和余数，分别保存在ax和cx中

 			push cx
			;余数入栈
			inc bx
			;当循环终止的时候可以进行弹栈存储操作了，但是我们需要一个标记，来标识我们需要
			;弹出多少次，我们使用bx来进行存储
			mov cx, ax
			add cx, dx
			;ax中的值在下一次运算中一定会用到，dx中的值也有可能会用到（当被除数很大时）
			;此时可以临时保存数据的只有cx了，因此我们直接将运算结果放到cx中
			;一举两得
			;判断条件是商==0，因此我么需要高16位和低16位全都为0
			jcxz ok1
			;处理过程是需要循环的，循环结束的条件是商==0 
			;我们只需要将执行jcxz指令即可，当cx的值位0的时候，它会自动跳转到ok1循环的
			jmp short split 
ok1:		pop ax
			add al, 30h
			mov byte ptr es:[di], al
			inc di
			dec bx
			mov cx, bx
			jcxz last
			jmp short ok1

last:		;最后一步，在数据的ASCII数据形式的最后加上一个0
			mov ah, 0
			mov byte ptr es:[di], ah 
			inc di

			pop es
			pop si
			pop ax 
			pop ds
			pop dx
			pop cx
			pop bx
			ret

divdw:		push ds
			push dx
			push cx
			push bx
			push ax 

			mov ax, temp
			mov ds, ax 
			mov ax, dx 
			mov dx, 0 
			div cx
			;ax存放商，dx存放余数
			;根据公式，使用被除数高位除以除数得到的商×65536
			;*65536等价于在低位加16个0，因此操作就会变得非常简单
			;使用被除数高位除以除数得到的余数×65536+被除数的低位，再将得到的结果除以除数
			;两者的结果相加，即可得到32位/16位的无溢出结果
			push dx
			;使用栈临时保存余数
			mov dx, ax
			mov ax, 0
			mov ds:[0], ax
			mov ds:[2], dx
			pop dx
			;弹出余数，作为右操作数中被除数的高16位
			pop ax
			;得到被除数的低16位
			push ax
			;恢复栈顶数据，避免对主程序造成干扰 
			;将右操作数[]中的左操作数的低16位和被除数的低16位相加
			;但是右操作数[]中的左操作数的低16位一定是全0的，因此我们可以省略这一步
			;直接执行pop ax，将被除数的低16位作为右操作数的低16位
			div cx
			;ax存放商，dx存放余数
			;由于左操作数的低16位一定是全0，所以不必与其相加，直接将
			;右操作数的低16位存储到ds:[0]内存单元即可
			mov ds:[0], ax
			;商的低16位放到ds:[0]单元中
			mov ds:[4], dx  
			;余数放到ds:[4]单元中
			;ds:[2]中一直保存的都是商的高16位，且没有被更改过，因此无须任何操作
	
			pop ax
			pop bx
			pop cx
			pop dx
			mov ax, ds:[0]
			;ax保存商的低16位
			mov dx, ds:[2]
			;dx保存商的高16位
			mov cx, ds:[4]
			;cx保存余数  
			pop ds
			;之所以要在pop ds之前将数据转移，是因为子程序divdw调用前，ds已经被使用
			;指向的是其他的段，如果不在pop之前转移数据，那么div段的数据就无法获取了
			ret	 

show_str:	push ax
			push bx
			push cx
			push dx
			push es 
			push ds
			;根据上节中的框架，为了不让子程序干扰主程序中寄存器的值，将所有子程序会用到的寄存器进行压栈
			mov ax, 0b800h
			mov es, ax
			;颜色区的段地址
			mov ax, show
			mov ds, ax
			;要读入的数据区的段地址
			mov al, 160 
			mul dh
			;每行占160个字节，乘以行数
			push ax
			;将行计算的结果存储到栈中
			mov al, 2
			mul dl
			;每列占2个字节，乘以列数
			pop bx
			;将上次运算的结果（160×行数）的值转移到bx中
			add bx, ax  ;此时的ax值为（2×列数）
			;将两者相加，最终结果保存到bx中
			mov dl, 130
			;显示颜色为绿色
change:		mov cl, ds:[di]
			inc di 
			mov ch, 0
			jcxz ok
			mov ch, dl
			mov es:[bx+si], cx
			add si, 2 
			jmp short change

ok:			pop ds 
			pop es
			pop dx
			pop cx
			pop bx
			pop ax
			ret

codesg ends
end start

codesg ends
end start

codesg ends
end start
```

## 中断

### 1. 分类

所谓**中断**，指的是CPU不再接着**刚执行完的指令**往下执行，而是转而处理由CPU内部或者外部产生的一种**特殊信息**。这种特殊信息我们称之为**中断信息**。中断信息要求CPU马上进行某种处理，并对所要进行的处理提供**必备参数**的通知信息。

中断信息可以来自外部，也可以来自内部。

我们通常将来自内部的中断称为**异常**，而把来自外部的中断称为**中断**。

### 2. 机制

#### 内中断

对于8086CPU来说，当CPU内部有下面的情况发生时，将产生相应的中断信息——

1. **除法错误**。如执行除法指令产生的除法溢出
2. **单步执行**
3. 执行**into指令**
4. 执行**int指令**

那么CPU接收到中断信息以后，会做出怎样的响应呢?

CPU首先会判断中断信息的来源，因而中断信息中必须包含有能够识别中断来源的编码，这种编码就是**中断类型码**。

CPU收到中断信息后，根据中断类型码会触发特定的**中断处理程序**，而要定位一段程序，就需要知道这段程序的**入口地址**。显然中断信息和其处理程序入口地址之间存在着某种**联系**。

这种联系就是中断向量表。CPU可以通过8位的中断类型码通过中断向量表找到中断处理程序的入口地址。

中断向量表，就是中断向量的列表。我们通常将具有大小和方向两个性质的几何对象称为**向量**，这里的中断向量**指的就是**中断处理程序的入口地址。

中断向量表在**内存**中保存，其中存放了256个中断源所对应中断处理程序的入口，对于8086CPU来说，中断向量表**指定**放在内存地址0处，从内存**0000:0000**到**0000:03FF**的1024个单元中存放着中断向量表。

那么在中断向量表中，一个表项占多大的空间呢？一个表项存放一个中断向量，也就是一个中断处理程序的入口地址，对于8086CPU来说，这个入口地址包括段地址和偏移地址，所以一个表项占2个字，高地址存放段地址，低地址存放偏移地址。存储**N号中断源**所对应的中断处理程序的入口的**段地址**和**偏移地址**的内存单元地址分别是：**4N**，**4N+2**

有一个问题需要考虑：CPU在执行完中断处理程序后，应该返回原来的执行点继续执行下面的指令，所以在中断过程中，在设置CS:IP之前，还要将原来的CS:IP值保存起来。

下面我们来概括一下中断过程的机制——

> 1. 从中断信息中取得中断类型码
> 2. 标志寄存器中的值入栈
> 3. 设置标志寄存器TF和IF的值为0
> 4. CS，IP的内容入栈
> 5. 从内存单元为中断类型码×4和中断类型码*4+2的两个字单元中读取中断处理程序的入口设置IP和CS

## 端口与BIOS读写

待更新

------

参考目录

1. 王燕老师的《汇编语言》，如果想简单了解下汇编语言的小伙伴，入手这本书就对啦。

> 山高路远，静水深流

